// tag::apply[]
// tag::publish[]
plugins {
    id 'groovy-gradle-plugin'
// end::apply[]
    id 'maven-publish'
// tag::apply[]
}
// end::apply[]

group = 'com.myorg.conventions'
version = '1.0'

publishing {
    repositories {
        maven {
            // change to point to your repo, e.g. http://my.org/repo
            url = layout.buildDirectory.dir("repo")
        }
    }
}

tasks.named('publish') {
    dependsOn('check')
}

// end::publish[]

// tag::repositories-and-dependencies[]
repositories {
    gradlePluginPortal() // so that external plugins can be resolved in dependencies section
}

dependencies {
    implementation 'gradle.plugin.com.github.spotbugs.snom:spotbugs-gradle-plugin:4.6.2'
    testImplementation platform("org.spockframework:spock-bom:2.0-M5-groovy-3.0")
    testImplementation 'org.spockframework:spock-core'
    testImplementation 'org.spockframework:spock-junit4'
    testImplementation 'junit:junit:4.13.1'
}

tasks.named('test') {
    useJUnitPlatform()
}

// end::repositories-and-dependencies[]
